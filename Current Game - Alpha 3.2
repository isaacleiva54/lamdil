Volume 1 - Mechanics

Book 1 - Menu

Chapter 1 - Help

Include Menus by Emily Short.

Table of Basic Help Options
title	subtable (a table name)	description	toggle (a rule)
"Instructions for Play"	Table of Instruction Options	--

Table of Instruction Options
title	subtable	description	
"What to do with '[command prompt]'"	--	"The [command prompt] sign is where the game says, 'Okay, what do you want to do now?'  You may respond by typing an instruction -- usually an imperative verb, possibly followed by prepositions and objects.  So, for instance, LOOK, TAKE KEY, UNLOCK DOOR."	
"How to move"	--	"Communicate using compass directions: (GO NORTH). You omit the word GO and abbreviate the compass directions as (N = North, S = South, E = East, W = West) In some locations, IN and OUT as well as UP and DOWN will also be useful."	
"Objects"	--	"Throughout the game there will be assorted objects that you can do things with. Most importantly, you may TAKE or GET items, and (when you are tired of them) DROP them again. INVENTORY (abbreviated I) will list the items you are currently holding. [paragraph break]There are usually assorted things you may do with these objects. OPEN, CLOSE, WEAR, EAT, LOCK, and UNLOCK are especially common.[paragraph break]Occasionally, you will find that the game does not recognize the name of an object even though it has been described as being in the room with you. If this is the case, the object is just there for scenery, and you may assume that you do not need to interact with it."	

Understand "help" or "hint" or "hints" or "about" or "info" as asking for help.
Asking for help is an action out of world.
Carry out asking for help (this is the help request rule):
   now the current menu is the Table of Basic Help Options;
   carry out the displaying activity;
   clear the screen;
   try looking.

Chapter 2 - Compass

Include Basic Screen Effects by Emily Short.

When play begins:
	 center "[story title]";
	 center "[story headline]";
	 center "by [story author]";
	 leave space;
	 center "Press SPACE to begin.";
	 wait for the SPACE key;
	 clear the screen;
	 leave space.
	
To turn screen black:
	 say white letters;
	 turn the background black;
	 clear the screen;
	 leave space;

To turn screen white:
	 turn the background white;
	 say black letters;
	 clear the screen;
	 leave space.

To leave space:
	 say paragraph break;
	 say paragraph break;
	 say paragraph break;
	 say paragraph break.

Table of Fancy Status
left	central	right 
"[if in darkness]Darkness[otherwise][location][end if]"	""	"[top rose]" 
"[left hand status line]"	"You [status of the player] (Health: [current hit points of player])[line break]"	"[middle rose]" 
"[right hand status line]"	""	"[bottom rose]" 


To say red reverse:
	 turn the background red.

To say black reverse:
	 turn the background black.

To say white reverse:
	 turn the background white.
	
To say rose (way - a direction):
	 let place be the room way from the location;
	 if the place is a room, say "[if the place is unvisited][red reverse][end if][way abbreviation][default letters]"; otherwise say "[way spacing]";
	
To say (way - a direction) abbreviation:
	 choose row with a chosen way of way in the Table of Various Directions;
	 say abbrev entry.

To say (way - a direction) spacing:
	 choose row with a chosen way of way in the Table of Various Directions;
	 say spacing entry.

Table of Various Directions
chosen way	abbrev	spacing 
up	"U "	" " 
northwest	"NW"	" " 
north	" N "	" " 
northeast	"NE"	" " 
east	" E"	" " 
west	"W "	" " 
southeast	"SE"	" " 
south	" S "	" " 
southwest	"SW"	" " 
down	"D "	" " 

To say top rose:
	 say "[rose up][rose northwest][rose north][rose northeast]".

To say middle rose:
	 say " [rose west] . [rose east]";

To say bottom rose:
	 say "[rose down][rose southwest][rose south][rose southeast]".

Rule for constructing the status line:
	 fill status bar with Table of Fancy Status;
	 say default letters;
	 rule succeeds.
	
Chapter 3 - Player Status

To say status of the player:
	let x be the current hit points of the player;
	if x is 20:
		say "are in perfect health";
	otherwise if x is at most 0:
		say "are dead";
	otherwise if x is at most 2:
		say "are near death";
	otherwise if x is at most 4:
		say "are in serious condition";
	otherwise if x is at most 6:
		say "have taken quite a beating";
	otherwise if x is at most 12:
		say "have definitely been better, but you could be worse";
	otherwise if x is at most 14:
		say "are a little beaten up";
	otherwise if x is at most 16:
		say "can almost hide the fact that you have been in a fight";
	otherwise if x is at most 18:
		say "have some scrapes and bruises, but you have looked worse";
	otherwise:
		say "are a little banged up, but still good-looking".
		
Book 2 - Consumables

Part 1 - Food

A food is a kind of thing. A food has a number called health restored.
Stale Bread is a food. The health restored of Stale Bread is 10.  

Instead of eating a food (called the consumable):
	if the current hit points of the player is less than 20 begin;
		say "You quickly gobble it down. Tasty. [health restored] health has been restored.";
		increase the current hit points of the player by health restored;
		remove the consumable from play;
	otherwise;
		say "You aren't nearly hungry enough to eat that right now. Save it for another time.";
	end if.

Book 3 - Combat

A person has a number called maximum hit points. A person has a number called current hit points.
Definition: a person is dead if their current hit points are less than 1. 
A weapon is a kind of thing. A weapon has a number called the maximum damage. A weapon has a number called current turns. A weapon has a number called multihits.
Understand the commands "fight" and "attack" and "punch" and "kill" and "murder" and "hit" and "thump" and "torture" and "stab" as something new. 
Attacking it with is an action applying to one visible thing and one carried thing. Understand "attack [someone] with [something preferably held]" as attacking it with. 
Understand the commands "fight" and "punch" and "kill" and "murder" and "hit" and "thump" and "torture" and "stab" as "attack". 
The attacking it with action has a number called the damage inflicted.
Check an actor attacking something with something (this is the can't attack with something that isn't a weapon rule): 
	if the second noun is not a weapon: 
		if the actor is the player, say "[The second noun] does not qualify as a weapon."; 
		stop the action. 
Check an actor attacking something with something (this is the can't attack a non-person rule): 
	if the noun is not a person: 
		if the actor is the player, say "[The noun] has no life to lose."; 
		stop the action. 

Part 1 - The Player

The current hit points of the player is 20. The maximum hit points of the player is 20.
Carry out the player attacking player with something (this is the standard attacking player with a weapon rule): 
	decrease the current hit points of the player by the damage inflicted; 
	if the player is dead:
		now the current hit points of the player is 0;
		end the story saying "You accidentally commit suicide".
	
Chapter 1 - Light Weapons

Dual Daggers is a thing. 

Dual Daggers is a weapon. The maximum damage of the Dual Daggers is 8. The multihits of the Dual Daggers is 1.
Setting action variables for attacking something with something: 
	if the second noun is a Dual Daggers: 
		let the maximum attack be the maximum damage of the second noun; 
		now the damage inflicted is a random number between 2 and the maximum attack. 	
Report attacking someone with something (this is the normal attacking with Dual Daggers 0 report rule):
	if second noun is a Dual Daggers:
		if the multihits of the Dual Daggers is 1:
			say "Then you swipe the Dual Daggers up, dealing [damage inflicted] more point[s] of damage!" instead.	
Carry out an actor attacking something with Dual Daggers (this is the standard attacking it with a Dual Daggers rule):
	if the noun is dead: 
		if the noun is not the player:
			now the noun is nowhere;
	if the multihits of the Dual Daggers is 1:
		decrease the current hit points of the noun by the damage inflicted; 
		say "You stab the Dual Daggers into [the noun], dealing [damage inflicted] point[s] of damage!";
		now the multihits of the Dual Daggers is 0;
	if the multihits of the Dual Daggers is 0:
		decrease the current hit points of the noun by the damage inflicted; 
		now the multihits of the Dual Daggers is 1;
		stop the action.
	
Chapter 2 - Medium Weapons

The player carries a weapon called Shank. The maximum damage of the Shank is 2.
Setting action variables for attacking something with something: 
	if the second noun is a Shank: 
		let the maximum attack be the maximum damage of the second noun; 
		now the damage inflicted is a random number between 1 and the maximum attack. 
Report attacking someone with something (this is the normal attacking with Shank report rule):
	if second noun is a Shank:
		say "You stab [the noun] with the shank, dealing [damage inflicted] point[s] of damage!" instead. 
Carry out an actor attacking something with Shank (this is the standard attacking it with a Shank rule): 
	decrease the current hit points of the noun by the damage inflicted; 
	if the noun is dead: 
		if the noun is not the player:
			now the noun is nowhere. 
			
Rusty Knife is a weapon. The maximum damage of the Rusty Knife is 3. 
Setting action variables for attacking something with something: 
	if the second noun is a Rusty Knife: 
		let the maximum attack be the maximum damage of the second noun; 
		now the damage inflicted is a random number between 3 and the maximum attack. 
Report attacking someone with something (this is the normal attacking with Rusty Knife report rule):
	if second noun is a Rusty Knife:
		say "You stab [the noun] with the rusty knife, dealing [damage inflicted] point[s] of damage!" instead. 
Carry out an actor attacking something with Rusty Knife (this is the standard attacking it with a Rusty Knife rule): 
	decrease the current hit points of the noun by the damage inflicted; 
	if the noun is dead: 
		if the noun is not the player:
			now the noun is nowhere. 
			
Broken Sword is a weapon. The maximum damage of the Broken Sword is 10.
Setting action variables for attacking something with something: 
	if the second noun is a Broken Sword: 
		let the maximum attack be the maximum damage of the second noun; 
		now the damage inflicted is a random number between 6 and the maximum attack. 
Report attacking someone with something (this is the normal attacking with Broken Sword report rule):
	if second noun is a Broken Sword:
		say "You slash at [the noun] with the broken sword, dealing [damage inflicted] point[s] of damage!" instead. 
Carry out an actor attacking something with Broken Sword (this is the standard attacking it with a Broken Sword rule): 
	decrease the current hit points of the noun by the damage inflicted; 
	if the noun is dead: 
		if the noun is not the player:
			now the noun is nowhere. 
	
Chapter 3 - Heavy Weapons

Diamond Pickaxe is a weapon. The maximum damage of the Diamond Pickaxe is 20. The current turns of the Diamond Pickaxe is 1.
Setting action variables for attacking something with something: 
	if the second noun is a Diamond Pickaxe: 
		let the maximum attack be the maximum damage of the second noun; 
		now the damage inflicted is a random number between 18 and the maximum attack. 	
Report attacking someone with something (this is the normal attacking with Diamond Pickaxe report rule):
	if the second noun is a Diamond Pickaxe:
		if the current turns of the Diamond Pickaxe is 0:
			say "You swing the diamond pickaxe over your shoulder." instead;
		if the current turns of the Diamond Pickaxe is 1:
			say "You slam the diamond pickaxe onto [the noun], finding [damage inflicted] diamond[s]!" instead.
Carry out an actor attacking something with Diamond Pickaxe (this is the standard attacking it with a Diamond Pickaxe rule):
	if the noun is dead: 
		if the noun is not the player:
			now the noun is nowhere;
	if the current turns of the Diamond Pickaxe is 1:
		now the current turns of the Diamond Pickaxe is 0;
		stop the action;
	if the current turns of the Diamond Pickaxe is 0:
		decrease the current hit points of the noun by the damage inflicted; 
		now the current turns of the Diamond Pickaxe is 1;
		stop the action.
	
Part 2 - Enemy Mechanics
	
Chapter 1 - Inner Prison Enemies

The current hit points of the Deranged Prisoner is 3. The maximum hit points of the Deranged Prisoner is 3. 
The Deranged Prisoner carries a weapon called Broken Glass. The maximum damage of the Broken Glass is 2.
Setting action variables for attacking something with something: 
	if the second noun is a Broken Glass: 
		let the maximum attack be the maximum damage of the second noun; 
		now the damage inflicted is a random number between 1 and the maximum attack. 
Every turn when the player is in the Prison Staircase (this is the Deranged Prisoner-attack rule): 
	if the current hit points of the Deranged Prisoner is greater than 0, try the Deranged Prisoner attacking the player with a random weapon which is carried by the Deranged Prisoner. 
Report Deranged Prisoner attacking the player with something (this is the standard report Deranged Prisoner attacking the player with rule): 
	say "The Deranged Prisoner slashes at your flesh, dealing [damage inflicted] point[s] of damage!" instead. 	
Report attacking a dead Deranged Prisoner with something (this is the Deranged Prisoner's death-report priority rule): 
	say "The Deranged Prisoner writhes in pain as he expires." instead. 
Report Deranged Prisoner attacking the player with something when the player is dead (this is the player's-death by Deranged Prisoner priority rule):
	now the current hit points of the player is 0; 
	say "The Deranged Prisoner quickly stabs your throat."; 
	end the story saying "The blood drains out of your neck and you fall to the ground"; 
	stop the action.
Carry out Deranged Prisoner attacking someone with something (this is the standard Deranged Prisoner attacking it with rule): 
	decrease the current hit points of the noun by the damage inflicted.

The current hit points of the Mutated Prisoner is 15. The maximum hit points of the Mutated Prisoner is 15.
The Mutated Prisoner carries a weapon called Left Hand. The maximum damage of the Left Hand is 4.
The Mutated Prisoner carries a weapon called Right Hand. The maximum damage of the Right Hand is 4.
The Mutated Prisoner carries a weapon called Third Hand. The maximum damage of the Third Hand is 5.
The Mutated Prisoner carries a weapon called Bite. The maximum damage of the Bite is 4.
Setting action variables for attacking something with something: 
	if the second noun is a Left Hand: 
		let the maximum attack be the maximum damage of the second noun; 
		now the damage inflicted is a random number between 2 and the maximum attack. 
Setting action variables for attacking something with something: 
	if the second noun is a Right Hand: 
		let the maximum attack be the maximum damage of the second noun; 
		now the damage inflicted is a random number between 3 and the maximum attack. 
Setting action variables for attacking something with something: 
	if the second noun is a Third Hand: 
		let the maximum attack be the maximum damage of the second noun; 
		now the damage inflicted is a random number between 3 and the maximum attack. 
Setting action variables for attacking something with something: 
	if the second noun is a Bite: 
		let the maximum attack be the maximum damage of the second noun; 
		now the damage inflicted is a random number between 2 and the maximum attack. 
Every turn when the player is in the Prison Tunnel (this is the Mutated Prisoner-attack rule):
	if the current hit points of the Mutated Prisoner is greater than 0, try the Mutated Prisoner attacking the player with a random weapon which is carried by the Mutated Prisoner. 
Report Mutated Prisoner attacking the player with Left Hand (this is the standard report Mutated Prisoner attacking the player with Left Hand rule): 
	say "The Mutated Prisoner claws at you with his left hand, dealing [damage inflicted] point[s] of damage!" instead. 
Report Mutated Prisoner attacking the player with Right Hand (this is the standard report Mutated Prisoner attacking the player with Right Hand rule): 
	say "The Mutated Prisoner claws at you with his right hand, dealing [damage inflicted] point[s] of damage!" instead. 
Report Mutated Prisoner attacking the player with Third Hand (this is the standard report Mutated Prisoner attacking the player with Third Hand rule): 
	say "The Mutated Prisoner stretches out his third arm and slashes at you, dealing [damage inflicted] point[s] of damage!" instead. 
Report Mutated Prisoner attacking the player with Bite (this is the standard report Mutated Prisoner attacking the player with Bite rule): 
	say "The Mutated Prisoner bites you with his jagged teeth, dealing [damage inflicted] point[s] of damage!" instead. 
Report attacking a dead Mutated Prisoner with something (this is the Mutated Prisoner's death-report priority rule): 
	say "The Mutated Prisoner lets out a moan as he shrivels up on the floor." instead. 
Report Mutated Prisoner attacking the player with something when the player is dead (this is the player's-death by Mutated Prisoner priority rule):
	now the current hit points of the player is 0;
	say "The Mutated Prisoner grabs onto your head with all three arms and tears it clean off."; 
	end the story saying "Your blood is splattered all over the tunnel as the mutant feasts on your remains"; 
	stop the action.
Carry out Mutated Prisoner attacking someone with something (this is the standard Mutated Prisoner attacking it with rule): 
	decrease the current hit points of the noun by the damage inflicted.

The current hit points of the Warden is 25. The maximum hit points of the Warden is 25.
The Warden carries a weapon called Silver Sword. The maximum damage of the Silver Sword is 4.
Setting action variables for attacking something with something: 
	if the second noun is a Silver Sword: 
		let the maximum attack be the maximum damage of the second noun; 
		now the damage inflicted is a random number between 2 and the maximum attack. 
Every turn when the player is in the Warden's Room (this is the Warden-attack rule):
	if the current hit points of the Warden is greater than 0, try the Warden attacking the player with a random weapon which is carried by the Warden. 
Report Warden attacking the player with something (this is the standard report Warden attacking the player with rule): 
	say "The Warden swings at you with his sword, dealing [damage inflicted] point[s] of damage!" instead. 
Report attacking a dead Warden with something (this is the Warden's death-report priority rule): 
	say "The floor trembles as the Warden collapses to the ground." instead. 
Report Warden attacking the player with something when the player is dead (this is the player's-death by Warden priority rule):
	now the current hit points of the player is 0;
	say "The Warden draws back his sword and plunges it through your heart."; 
	end the story saying "The Warden's laughter echos throughout the prison as your vision fades to black."; 
	stop the action.
Carry out Warden attacking someone with something (this is the standard Warden attacking it with rule): 
	decrease the current hit points of the noun by the damage inflicted.
	
Book 4 - Breaking Walls

A breakable wall is a kind of thing. A breakable wall has a number called maximum break points. A breakable wall has a number called current break points. 
Definition: a breakable wall is broken if its current break points is less than 1. 
A wall breaker is a kind of thing. A wall breaker has a number called the maximum break damage.
Understand the commands "break" and "destroy" and "smash" and "wreck" and "demolish" as something new. 
Breaking it with is an action applying to one visible thing and one carried thing. Understand "break [breakable wall] with [something preferably held]" as breaking it with. 
Understand the commands "destroy" and "smash" and "wreck" and "demolish" as "break".
Breaking it with action has a number called the break damage.
Check an actor breaking something with something (this is the can't break with something that isn't a wall breaker rule): 
	if the second noun is not a wall breaker: 
		if the actor is the player, say "You can't break walls with [the second noun]."; 
		stop the action. 	
Check an actor breaking something with something (this is the can't break a non-breakable wall rule): 
	if the noun is not a breakable wall: 
		if the actor is the player, say "You can't break that."; 
		stop the action. 

Part 1 - Wall Breakers

Rock Hammer is a wall breaker. The maximum break damage of the Rock Hammer is 1.
Setting action variables for breaking something with something: 
	if the second noun is a Rock Hammer: 
		let the maximum breakage be the maximum break damage of the second noun; 
		now the break damage is a random number between 1 and the maximum breakage. 
	
Part 2 - Walls

The Tunnel Wall is a breakable wall. The maximum break points of the Tunnel Wall is 1. The current break points of the Tunnel Wall is 1.
Report breaking Tunnel Wall with something (this is the normal breaking Tunnel Wall report rule): 
	say "You slam the wall with great force." instead. 
Carry out an actor breaking Tunnel Wall with something (this is the standard breaking Tunnel Wall with a wall breaker rule): 
	decrease the current break points of the noun by the break damage; 
	if the noun is broken:
		now the noun is nowhere;
		move Diamond Pickaxe to Tunnel Extension.
		
The Prison Wall is a breakable wall. The maximum break points of the Prison Wall is 1. The current break points of the Prison Wall is 1.
Report breaking Prison Wall with something (this is the normal breaking Prison Wall report rule): 
	say "You strike the cracked wall." instead. 
Carry out an actor breaking Prison Wall with something (this is the standard breaking Prison Wall with a wall breaker rule): 
	decrease the current break points of the noun by the break damage; 
	if the noun is broken:
		now the noun is nowhere.

Book 5 - Inventory

Part 1 - Carrying Capacity

The carrying capacity of the player is 5.

Instead of taking Guard's Satchel: 
	let inventory space be carrying capacity of the player;
	now the carrying capacity of the player is inventory space plus 5;
	say "You can now hold 5 more items.";
	remove the noun from play.
	
Volume 2 - Story 

Book 1 - Inner Prison

Part 1 - Prison Chapter

When play begins:
	now the left hand status line is "";
	now the right hand status line is "".

Prison Chapter is a scene.
Prison Chapter begins when the Player is in Cell 1.

Cell 1 is a room. "You wake up in your cell".

Rule for printing the name of Cell 1:
	say "Cell".

Note is a thing in Cell 1. The description of the Note is "If you ever get lost or don't know what to do, use the Help command."
Walkway 1 is north of Cell 1. Walkway 1 is a room. "A strange silence fills the air."

Rule for printing the name of Walkway 1:
	say "Walkway".

Walkway 2 is east of Walkway 1. Walkway 2 is a room. "Oddly, the cell here is open."

Rule for printing the name of Walkway 2:
	say "Walkway".

Cell 2 is south of Walkway 2. Cell Key is in Cell 2. Cell 2 is a room. "Someone used to stay here."

Rule for printing the name of Cell 2:
	say "Cell".

The Cell Door is south of Walkway 3. The Cell Door is a door. The Cell Door is scenery. The Cell Door is locked. Cell Key unlocks the Cell Door. Walkway 3 is east of Walkway 2. Walkway 3 is a room. "[if Rusty Knife is in Cell 3]There is something in the nearby cell."

Rule for printing the name of Walkway 3:
	say "Walkway".

Cell 3 is south of Cell Door. Rusty Knife is in Cell 3. Cell 3 is a room. "[if Rusty Knife is in Cell 3]The guy in this cell was always the violent one."

Rule for printing the name of Cell 3:
	say "Cell".

Walkway 4 is east of Walkway 3. Walkway 4 is a room. "[if Deranged Prisoner is not dead]You hear someone muttering quietly."

Rule for printing the name of Walkway 4:
	say "Walkway".

Instead of listening to Walkway 4:
	if Deranged Prisoner is not dead:
		say "I'll kill those sons of bitches... I'll kill 'em...";
	otherwise:
		say "You hear nothing unexpected.".

Cell 4 is south of Walkway 4. Cell 4 is a room. "There is a hole big enough for a person to fit through.[if Mutated Prisoner is not dead] You probably shouldn't go down."

Rule for printing the name of Cell 4:
	say "Cell".

Instead of listening to Cell 4:
	if Mutated Prisoner is not dead:
		say "You hear something growling.";
	otherwise:
		say "You hear nothing unexpected.".

Prison Tunnel is below Cell 4. Prison Tunnel is a room. The Mutated Prisoner is a man in the Prison Tunnel. "[if Mutated Prisoner is not dead]A man with three arms instantly turns around and attacks you!"

Instead of listening to Prison Tunnel:
	if Mutated Prisoner is not dead:
		say "The Mutated Prisoner screams at you.";
	otherwise:
		say "You hear nothing unexpected.".

Prison Tunnel Fight is a recurring scene.
Prison Tunnel Fight begins when the player is in the Prison Tunnel.
Every turn during the Prison Tunnel Fight:
	now the left hand status line is "Mutated Prisoner: [current hit points of Mutated Prisoner]";
	if the Mutated Prisoner is dead:
		now the left hand status line is "Mutated Prisoner: Deceased";
		now the Mutated Prisoner is nowhere.
Prison Tunnel Fight ends when the player is not in the Prison Tunnel.	
When Prison Tunnel Fight ends:
	now the left hand status line is "".
	
The Tunnel Extension is south of Prison Tunnel. Tunnel Extension is a room. "[if Tunnel Wall is not broken]You knock on the wall of the tunnel. It sounds hollow." The Tunnel Wall is in Tunnel Extension. The Tunnel Wall is scenery. Diamond Pickaxe is a thing.

Tunnel Wall Breaking is a scene.
Tunnel Wall Breaking begins when Tunnel Wall is broken.
When Tunnel Wall Breaking begins:
	say "The wall crumbles to reveal a diamond pickaxe.".
Tunnel Wall Breaking ends when Tunnel Wall is nowhere.
	
The Prison Staircase is east of Walkway 4. Prison Staircase is a room. A Deranged Prisoner is a man in the Prison Staircase. "[if Deranged Prisoner is not dead]Quick! A deranged prisoner is charging at you!"

Instead of listening to Prison Staircase:
	if Deranged Prisoner is not dead:
		say "Deranged Prisoner: I'll kill you!";
	otherwise:
		say "You hear nothing unexpected.".

Prison Staircase Fight is a recurring scene.
Prison Staircase Fight begins when the player is in the Prison Staircase.
Every turn during Prison Staircase Fight: 
	now the left hand status line is "Deranged Prisoner: [current hit points of Deranged Prisoner]";
	if the Deranged Prisoner is dead:
		now the left hand status line is "Deranged Prisoner: Deceased";
		now the Deranged Prisoner is nowhere.
Prison Staircase Fight ends when the player is not in the Prison Staircase.
When Prison Staircase Fight ends:
	now the left hand status line is "".
	
Prison Chapter ends when the Deranged Prisoner is dead.

Part 2 - Captain's Chapter

Captain's Chapter is a scene.
Captain's Chapter begins when Prison Chapter ends.

The Prison Staircase is below the Captain's Staircase. The Captain's Staircase is a room above the Prison Staircase. "There is no soul in sight[if Warden is not dead] yet the air is warm as if someone just hurried away in a rush[end if]."
The Guard's Room is west of the Captain's Staircase. The Guard's Room is a room. "You see beds and tables sprawled across the room. There are chests full of private belongings."
In the Guard's Room is an openable container called Guard's Chest. Guard's Chest is closed. The Guard's Chest is fixed in place. "[if open]The Guard's Chest is open.[else]The Guard's Chest is closed." In Guard's Chest is a thing called Iron Key. In Guard's Chest is a thing called Stale Bread. In Guard's Chest is a thing called Guard's Satchel.

Rule for printing the name of Guard's Chest: 
	say "[printed name of Guard's Chest]";
	omit contents in listing.
	
The Iron Door is east of the Captain's Staircase and west of the Armoury. The Iron Door is a door. The Iron Door is scenery. The Iron Door is locked. Iron Key unlocks the Iron Door. The Armoury is a room. "You see only a few weapons and pieces of gear around the room. You conclude the room was looted." In the Armoury is Broken Sword and Wooden Shield.
North of the Captain's Staircase is a Hallway. The Hallway is a room. Flaming Torch is in the Hallway. Flaming Torch is lit. "The room up ahead is dark.[if Flaming Torch is in Hallway] It would be wise to take the flaming torch."
North of the Hallway is the Warden's Staircase. The Warden's Staircase is a dark room. "[if Warden is not dead]Above you hear the sounds of chains rattling and heavy thuds running across the floor."

Instead of listening to Warden's Staircase:
	if Warden is not dead:
		say "You hear rattling chains and heavy thuds.";
	otherwise:
		say "You hear nothing unexpected.".

Captain's Chapter ends when the Player is in the Warden's Staircase.

Part 3 - Warden's Chapter

Warden's Chapter is a scene.
Warden's Chapter begins when Captain's Chapter ends.

The Warden's Staircase is below the Warden's Room. Above the Warden's Staircase is a room called the Warden's Room. 
The Warden is a man in the Warden's Room. "[if Warden is not dead]Warden: Nobody escapes this prison."

Instead of listening to Warden's Room:
	if Warden is not dead:
		say "Warden: Traitors like you do not deserve to live!";
	otherwise:
		say "You hear nothing unexpected.".

Warden's Room Fight is a recurring scene.
Warden's Room Fight begins when the player is in the Warden's Room.
Every turn during Warden's Room Fight: 
	now the left hand status line is "Warden: [current hit points of Warden]";
	if the Warden is dead:
		now the left hand status line is "Warden: Deceased";
		now the Prison Door is unlocked;
		say "The prison door is now open.";
		now the Warden is nowhere.
Warden's Room Fight ends when the player is not in the Warden's Room.
When Warden's Room Fight ends:
	now the left hand status line is "".

The Prison Door is north of the Warden's Room. The Prison Door is a door. The Prison Door is scenery. The Prison Door is locked. The Prison Entrance is north of the Prison Door. The Prison Entrance is a room. "Its been awhile since you've seen sunlight."

Warden's Chapter ends when the Warden is dead.

Part 3 - Yard Chapter

Yard Chapter is a scene.
Yard Chapter begins when Warden's Chapter ends.

Prison Yard 5 is north of the Prison Entrance. Prison Yard 5 is a room. "The walls surrounding the prison are preventing your escape."

Rule for printing the name of Prison Yard 5:
	say "Prison Yard". 

Prison Yard 4 is west of Prison Yard 5. Prison Yard 4 is a room. "There is a wall here."

Rule for printing the name of Prison Yard 4:
	say "Prison Yard". 

Prison Yard 6 is east of Prison Yard 5. Prison Yard 6 is a room. "There is a wall here."

Rule for printing the name of Prison Yard 6:
	say "Prison Yard". 

Prison Yard 3 is north of Prison Yard 6. Prison Yard 3 is a room. "[if Rock Hammer is in Prison Yard 3]There is something in the grass." Rock Hammer is in Prison Yard 3. 

Rule for printing the name of Prison Yard 3:
	say "Prison Yard". 

Prison Yard 2 is east of Prison Yard 1 and north of Prison Yard 5 and west of Prison Yard 3. Prison Yard 2 is a room. "Freedom is within your grasp..." The Prison Gate is north of Prison Yard 2. The Prison Gate is a door. The Prison Gate is scenery. The Prison Gate is locked.

Rule for printing the name of Prison Yard 2:
	say "Prison Yard". 

Prison Yard Fake Exit is north of Prison Gate. Prison Yard Fake Exit is a room.
Prison Yard 1 is north of Prison Yard 4. Prison Yard 1 is a room. "[if Prison Wall is not broken]The wall here is a bit cracked.[else]You can now escape the prison." The Prison Wall is in Prison Yard 1. The Prison Wall is scenery.

Rule for printing the name of Prison Yard 1:
	say "Prison Yard". 

Prison Yard Placeholder is north of Prison Yard 1. Prison Yard Placeholder is a room.

Rule for printing the name of Prison Yard Placeholder:
	say "".

Prison Wall Breaking is a scene.
Prison Wall Breaking begins when Prison Wall is broken.
When Prison Wall Breaking begins:
	say "The wall gives away to reveal the outside world".
Prison Wall Breaking ends when Prison Wall is nowhere.

Instead of going north when the player is in Prison Yard 1:
	if Prison Wall is not broken:
		say "You can't go that way.";
		move player to Prison Yard 1;
	otherwise:
		move player to Prison Yard Placeholder;
		move player to Island.

Yard Chapter ends when Prison Wall is broken.

Book 2 - Prison Island

Part 1 - Island Chapter

Island Chapter is a scene.
Island Chapter begins when Yard Chapter ends.

Island is north of Prison Yard Placeholder. Island is a room.

Instead of going south when the player is in Island:
	move player to Prison Yard 1.
